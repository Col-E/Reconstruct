plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'me.darknet'
version '0.0.1.2'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    testImplementation.extendsFrom compileOnly
}

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll(['-parameters', '-g:lines,source,vars'])
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

dependencies {
    def asm = '9.3'
    def sim = '1.9.2'
    // objectweb asm
    api "org.ow2.asm:asm:${asm}"
    api "org.ow2.asm:asm-commons:${asm}"
    api "org.ow2.asm:asm-tree:${asm}"
    api "org.ow2.asm:asm-analysis:${asm}"
    api "org.ow2.asm:asm-util:${asm}"
    api "com.github.Col-E:SimAnalyzer:${sim}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = 'sources-repo'
            url = 'file:///tmp/sources-repo'
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}